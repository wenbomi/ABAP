目的：使用SM30维护表数据后，会在CDHDR和CDPOS表 或 RSSCD100事务中记录操作记录

先创建好SCDO中的维护对象，这里参考**Change Document**文档，完成创建步骤

事务码SE11->表维护生成器->环境->修改->事件

<img src="../../../../Library/Application%20Support/typora-user-images/image-20240617082701221.png" alt="image-20240617082701221" style="zoom:33%;" />

<img src="../../../../Library/Application%20Support/typora-user-images/image-20240617082736962.png" alt="image-20240617082736962" style="zoom:50%;" />

创建两个子例程 类型分别是 01 和 02 ，例程名称自定义

<img src="../../../../Library/Application%20Support/typora-user-images/image-20240617082859348.png" alt="image-20240617082859348" style="zoom:50%;" />

点击编辑器维护代码部分

第一个01的代码维护，创建对应的方法模块

```abap
INCLUDE fzcd_studentscdt.
INCLUDE fzcd_studentscdc.

FORM change_entry.
TYPES: BEGIN OF ty_tcdrp,
             object     TYPE cdobjectcl,
             reportname TYPE cdreport,
           END OF   ty_tcdrp,
           BEGIN OF ty_view_tab,
             object  TYPE cdobjectcl,
             tabname TYPE cdtabname,
           END OF   ty_view_tab.
    DATA: lt_ptab      TYPE STANDARD TABLE OF string,
          lv_prog      TYPE string,
          lv_mess      TYPE string,
          lin          TYPE i,
          lv_sid       TYPE string,
          lt_obj       TYPE STANDARD TABLE OF ty_view_tab,
          lt_tcdrp     TYPE STANDARD TABLE OF ty_tcdrp,
          lv_fugn      TYPE funct_pool,
          lv_table     TYPE cdtabname,
          lv_namesfunc TYPE namespace,
          lv_funcgroup TYPE progname,
          lv_namesprog TYPE namespace,
          lv_program   TYPE progname,
          lrt_tabname  TYPE RANGE OF tabname,
          lt_dd26v     TYPE TABLE OF dd26v,
          lv_object    TYPE cdobjectcl.
    " Get tabnames
    " DD: Interface for reading a view from the ABAP/4 Dictionary
    CALL FUNCTION 'DDIF_VIEW_GET'
      EXPORTING
        name          = vim_view_name
      TABLES
        dd26v_tab     = lt_dd26v
      EXCEPTIONS
        illegal_input = 1
        OTHERS        = 2.
    IF sy-subrc IS NOT INITIAL.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
              WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    ENDIF.
    IF lt_dd26v IS INITIAL.
      APPEND INITIAL LINE TO lrt_tabname ASSIGNING FIELD-SYMBOL(<lrs_tabname>).
      <lrs_tabname>-sign   = 'I'.
      <lrs_tabname>-option = 'EQ'.
      <lrs_tabname>-low    = vim_view_name.
    ELSE.
      SORT lt_dd26v BY tabname.
      DELETE ADJACENT DUPLICATES FROM lt_dd26v COMPARING tabname.
      "*-
      LOOP AT lt_dd26v INTO DATA(ls_dd26v).
        APPEND INITIAL LINE TO lrt_tabname ASSIGNING <lrs_tabname>.
        <lrs_tabname>-sign   = 'I'.
        <lrs_tabname>-option = 'EQ'.
        <lrs_tabname>-low    = ls_dd26v-tabname.
      ENDLOOP.
    ENDIF.
    " Objects for change document creation
    SELECT object tabname
      FROM tcdob
      INTO TABLE lt_obj
      WHERE tabname IN lrt_tabname
      ##WARN_OK.
    IF sy-subrc IS NOT INITIAL.
      " No change document objects found
      MESSAGE i899(cd).
      RETURN.
    ENDIF.
    " Information on Include Reports Generated by RSSCD000
    SELECT object reportname
      FROM tcdrp
      INTO TABLE lt_tcdrp
      FOR ALL ENTRIES IN lt_obj
      WHERE object EQ lt_obj-object
      ##WARN_OK.
    IF sy-subrc IS NOT INITIAL.
      " Update program does not yet exist
      MESSAGE i446(m2).
      RETURN.
    ENDIF.
    " View Directory
    SELECT SINGLE area
      FROM tvdir
      INTO lv_fugn
      WHERE tabname EQ vim_view_name.
    "*-
    LOOP AT lt_obj ASSIGNING FIELD-SYMBOL(<ls_obj>).
      READ TABLE lt_tcdrp ASSIGNING FIELD-SYMBOL(<ls_tcdrp>)
                          WITH KEY object = <ls_obj>-object.
      IF sy-subrc IS NOT INITIAL.
        CONTINUE.
      ENDIF.
      " Split namespace
      CLEAR: lv_namesprog, lv_program.
      lv_program = <ls_tcdrp>-reportname.
      CALL FUNCTION 'RS_NAME_SPLIT_NAMESPACE'
        EXPORTING
          name_with_namespace    = lv_program
        IMPORTING
          namespace              = lv_namesprog
          name_without_namespace = lv_program
        EXCEPTIONS
          delimiter_error        = 1
          OTHERS                 = 2.
      IF sy-subrc <> 0.
        lv_program = <ls_tcdrp>-reportname.
      ENDIF.
      CLEAR: lv_namesfunc, lv_funcgroup.
      lv_funcgroup = lv_fugn.
      CALL FUNCTION 'RS_NAME_SPLIT_NAMESPACE'
        EXPORTING
          name_with_namespace    = lv_funcgroup
        IMPORTING
          namespace              = lv_namesfunc
          name_without_namespace = lv_funcgroup
        EXCEPTIONS
          delimiter_error        = 1
          OTHERS                 = 2.
      IF sy-subrc <> 0.
        lv_funcgroup = lv_fugn.
      ENDIF.
      " Namespace conversion
      lv_object = <ls_obj>-object.
      IF lv_object(1) = '/'.
        SHIFT lv_object LEFT DELETING LEADING '/'.
        REPLACE FIRST OCCURRENCE OF '/' IN lv_object WITH '_'.
      ENDIF.
      lv_table = <ls_obj>-tabname.
      IF lv_table(1) = '/'.
        SHIFT lv_table LEFT DELETING LEADING '/'.
        REPLACE FIRST OCCURRENCE OF '/' IN lv_table WITH '_'.
      ENDIF.
      
      PERFORM f_process.

    ENDLOOP.
  ENDIF.

ENDFORM.
```

```abap
*&---------------------------------------------------------------------*
*& Form f_process
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM f_process .
  TYPES:  BEGIN OF total.
            INCLUDE STRUCTURE zmwb_students.
            INCLUDE STRUCTURE vimflagtab.
  TYPES:  END OF total.
  FIELD-SYMBOLS: <fs_total>       TYPE ANY TABLE,
                 <fs_total_wa>    TYPE total,
                 <fs_x_namtab>    TYPE ANY TABLE,
                 <fs_x_namtab_wa> TYPE vimnamtab,
                 <fs_field>       TYPE any.
  DATA: lv_tabname(40) TYPE c,
        lv_cond_line   TYPE string,
        lv_cond_line2  TYPE string.

  LOOP AT total .
    CASE <action>.
      WHEN 'U'. " Update
        lv_tabname = '(SAPLZFG_XM01)X_NAMTAB[]'. "更改位置 填入表维护所在的函数组ZFG_XM01"
        ASSIGN (lv_tabname) TO <fs_x_namtab>.
        lv_cond_line2 = |keyflag EQ 'X' AND viewfield NE 'MANDT'|.
        LOOP AT <fs_x_namtab> ASSIGNING <fs_x_namtab_wa> WHERE (lv_cond_line2).
          ASSIGN COMPONENT <fs_x_namtab_wa>-viewfield OF STRUCTURE <vim_total_struc>  TO <fs_field>.
          IF sy-subrc IS INITIAL.
            lv_cond_line = lv_cond_line && |AND | &&
            <fs_x_namtab_wa>-viewfield && | EQ '| && <fs_field> && |' |.
            objectid = objectid && <fs_field>.
            UNASSIGN <fs_field>.
          ENDIF.
        ENDLOOP.
        IF sy-subrc IS INITIAL.
          SHIFT lv_cond_line LEFT BY 3 PLACES.
          " 旧值 要更改的地方
          SELECT  *
          FROM zmwb_students
          INTO TABLE yzmwb_students
          WHERE (lv_cond_line).
          MOVE-CORRESPONDING <vim_total_struc>  TO xzmwb_students.
          APPEND xzmwb_students.
          
          objectid        = objectid.
          tcode           = sy-tcode.
          udate           = sy-datum.
          utime           = sy-uzeit.
          username        = sy-uname.
          cdoc_upd_object = 'U'.
          upd_zmwb_students = 'U'.
          PERFORM cd_call_zcd_students.
        ENDIF.
      WHEN 'N'. " New
        lv_tabname = '(SAPLZFG_XM01)X_NAMTAB[]'."更改位置 填入表维护所在的函数组ZFG_XM01"
        ASSIGN (lv_tabname) TO <fs_x_namtab>.
        lv_cond_line2 = |keyflag EQ 'X' AND viewfield NE 'MANDT'|.
        LOOP AT <fs_x_namtab> ASSIGNING <fs_x_namtab_wa> WHERE (lv_cond_line2).
          ASSIGN COMPONENT <fs_x_namtab_wa>-viewfield OF STRUCTURE <vim_total_struc>  TO <fs_field>.
          IF sy-subrc IS INITIAL.
            objectid = objectid && <fs_field>.
            UNASSIGN <fs_field>.
          ENDIF.
        ENDLOOP.
        IF sy-subrc IS INITIAL.
          MOVE-CORRESPONDING <vim_total_struc>  TO xzmwb_students.
          APPEND xzmwb_students.
          objectid        = objectid.
          tcode           = sy-tcode.
          udate           = sy-datum.
          utime           = sy-uzeit.
          username        = sy-uname.
          cdoc_upd_object = 'I'.
          upd_zmwb_students = 'I'.
          PERFORM cd_call_zcd_students.
        ENDIF.
      WHEN 'D'. " Delete
        lv_tabname = '(SAPLZFG_XM01)X_NAMTAB[]'."更改位置 填入表维护所在的函数组ZFG_XM01"
        ASSIGN (lv_tabname) TO <fs_x_namtab>.
        lv_cond_line2 = |keyflag EQ 'X' AND viewfield NE 'MANDT'|.
        LOOP AT <fs_x_namtab> ASSIGNING <fs_x_namtab_wa> WHERE (lv_cond_line2).
          ASSIGN COMPONENT <fs_x_namtab_wa>-viewfield OF STRUCTURE <vim_total_struc>  TO <fs_field>.
          IF sy-subrc IS INITIAL.
            objectid = objectid && <fs_field>.
            UNASSIGN <fs_field>.
          ENDIF.
        ENDLOOP.
        IF sy-subrc IS INITIAL.
          MOVE-CORRESPONDING <vim_total_struc>  TO yzmwb_students.
          APPEND yzmwb_students.
          objectid        = objectid.
          tcode           = sy-tcode.
          udate           = sy-datum.
          utime           = sy-uzeit.
          username        = sy-uname.
          cdoc_upd_object = 'D'.
          upd_zmwb_students = 'D'.
          PERFORM cd_call_zcd_students.
        ENDIF.
    ENDCASE.
    CLEAR: lv_cond_line, lv_cond_line2, objectid, zmwb_students,                                                *zmwb_students,xzmwb_students,yzmwb_students.
    REFRESH: xzmwb_students,yzmwb_students.
  ENDLOOP.
ENDFORM.
```

代码中的INCLUDE fzcd_studentscdt.、INCLUDE fzcd_studentscdc. 分别是创建change document时自动创建的

 <img src="../../../../Library/Application%20Support/typora-user-images/image-20240617083752241.png" alt="image-20240617083752241" style="zoom:50%;" />

PERFORM cd_call_zcd_students.该函数也是包含在INCLUDE fzcd_studentscdc中创建好的函数模块



02、模块的程序维护

```abap
FORM save_control.
  COMMIT WORK AND WAIT.
ENDFORM.
```



之后 通过sm30维护表数据 点击保存后 其变更过程就会被记录到表中
